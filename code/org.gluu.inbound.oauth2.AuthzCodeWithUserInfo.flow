Flow org.gluu.inbound.oauth2.AuthzCodeWithUserInfo
     Basepath ""
     Inputs oauthParams
// Launches the code flow
outcome = Trigger org.gluu.inbound.oauth2.AuthzCode oauthParams
// Extracts the access token required for requesting UserInfo
token = outcome.data.access_token
// Calls the userinfo endpoint and parses the request as a map
profile | E = Call org.gluu.util.NetworkUtils#mapFromGetRequestWithToken oauthParams.userInfoEndpoint token
When E is not null
     Log "@error " E
     msg = [ "Unable to retrieve user profile.", E.message ]
     // Build error message
     msg = Call java.lang.String#join " " msg
     outcome = { success: false, error: msg }
     // Fail
     Finish outcome
Log "@debug Profile data\n" profile
// Set variable with profile and token data
outcome = { success: true, data: { profile: profile, tokenResponse: outcome.data } }
// End happily
Finish outcome