Flow org.gluu.inbound.oauth2.AuthzCode
     Basepath ""
     Inputs oauthParams
When oauthParams.redirectUri is null
     // Obtain redirect uri based on hostname from HTTP url
     oauthParams.redirectUri = Call org.gluu.util.NetworkUtils#makeRedirectUri 
// Instantiate code grant utility
codeGrantUtil = Call org.gluu.inbound.oauth2.CodeGrantUtil#new oauthParams
Log "@info Creating an authorization request"
url = Call codeGrantUtil makeAuthzRequest 
state = url.second
url = url.first
Log "@info Redirecting to external site"
responseParams = RFAC url
Log "@info Browser taken back to redirect URI"
// Parse code presented at callback uri
code = Call codeGrantUtil parseCode responseParams state
response = Call codeGrantUtil getTokenResponse code
Log "@info An access token has been obtained"
outcome = { success: true, data: response }
Finish response